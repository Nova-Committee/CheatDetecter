plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id "com.replaymod.preprocess" version "SNAPSHOT"
    id 'maven-publish'
}

preprocess {
    def mc1182 = createNode("1.18.2", 1_18_02, "yarn")
    def mc1192 = createNode("1.19.2", 1_19_02, "yarn")
    def mc1193 = createNode("1.19.3", 1_19_03, "yarn")
    def mc1194 = createNode("1.19.4", 1_19_04, "yarn")
    def mc1201 = createNode("1.20.1", 1_20_01, "yarn")
    def mc1204 = createNode("1.20.4", 1_20_04, "yarn")

    mc1182.link(mc1192, null)
    mc1192.link(mc1193, null)
    mc1193.link(mc1194, null)
    mc1194.link(mc1201, null)
    mc1201.link(mc1204, null)
}

apply(plugin: "com.replaymod.preprocess")

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url "https://maven.nova-committee.cn/releases" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.shedaniel.me/" }

    maven {
        name("JitPack")
        url("https://www.jitpack.io")
    }

    maven {
        name("CurseForge Maven")
        url("https://www.cursemaven.com")

        content {
            includeGroup("curse.maven")
        }
    }

    maven{
        name("Modrinth Maven")
        url("https://api.modrinth.com/maven")

        content {
            includeGroup("maven.modrinth")
        }
    }

    mavenCentral()
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings(loom.officialMojangMappings())
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation("top.hendrixshen.magiclib:magiclib-${project.minecraft_version.replace(".", "_")}:${project.magiclib_version}") {
        exclude(group: "carpet", module: "fabric-carpet")
    }

//    modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

    compileOnly("org.projectlombok:lombok:1.18.24")
    annotationProcessor("org.projectlombok:lombok:1.18.24")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "minecraft_version_prefix": project.minecraft_version.replace(".", "_"),
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
    }
}
